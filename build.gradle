buildscript {
    repositories {
        jcenter()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:2.1.0'
      classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'id.co.veritrans'
version = '0.9.2'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    
    lintOptions {
          abortOnError false
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName version
        buildConfigField "String", "MIXPANEL_URL", "\"https://api.mixpanel.com/\""
        buildConfigField "String", "CALLBACK_STRING", "\"/token/callback/\""
        buildConfigField "String", "CALLBACK_URL", "\"https://hangout.betas.in/veritrans/api/paymentstatus\""
        buildConfigField "String", "BBM_MONEY_PACKAGE", "\"com.monitise.client.android.bbmmoney\""
        buildConfigField "String", "MARKET_URL", "\"market://details?id=\""
        buildConfigField "String", "PLAY_STORE_URL", "\"https://play.google.com/store/apps/details?id=\""
        buildConfigField "String", "BBM_PREFIX_URL", "\"bbmmoney://api/payment/imp?data=\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        development {
            buildConfigField "String", "BASE_URL", "\"https://api.sandbox.veritrans.co.id/v2/\""
            buildConfigField "String", "MIXPANEL_TOKEN", "\"cc005b296ca4ce612fe3939177c668bb\""
        }

        production {
            buildConfigField "String", "BASE_URL", "\"https://api.veritrans.co.id/v2/\""
            buildConfigField "String", "MIXPANEL_TOKEN", "\"0269722c477a0e085fde32e0248c6003\""
        }
    }

}

repositories {
  jcenter()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.google.android.gms:play-services:8.4.0'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'pt-midtrans'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/veritrans/androidsdk.git'
        desc = 'Android SDK for integrating with veritrans.co.id'
        websiteUrl = "https://veritrans.co.id"
        publish = true
        publicDownloadNumbers = true
    }
    publications = ['BintrayPublication']

}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/androidsdk-private-development-release.aar")
            groupId group
            artifactId 'androidsdk'
            version version
        }
    }
}