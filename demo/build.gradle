buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    // TODO: Change this to Firebase Crashlytics, will be deprecated in Nov 2020 and not recording error anymore
    dependencies {
        classpath 'io.fabric.tools:gradle:1.28.0'
    }
}

apply plugin: 'com.android.application'
// TODO: Change this to Firebase Crashlytics, will be deprecated in Nov 2020 and not recording error anymore
// apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.midtrans.demo"
        minSdkVersion rootProject.ext.uiMinSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 4
        versionName "1.21.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue "string", "app_name", "Midtrans Demo Dev"
    }

    signingConfigs {
        release {
            storeFile file("$KEYSTORE")
            storePassword "$KEYSTORE_PASSWORD"
            keyAlias "$KEY_ALIAS"
            keyPassword "$KEY_PASSWORD"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        development {
            dimension "env"
            applicationId "com.midtrans.sdk.demo.development"
            resValue "string", "app_name", "Midtrans Demo Dev"
            buildConfigField "String", "BASE_URL", MERCHANT_BASE_URL_SANDBOX
            buildConfigField "String", "CLIENT_KEY", MERCHANT_CLIENT_KEY_SANDBOX
            matchingFallbacks = ["development"]
        }

        production {
            dimension "env"
            applicationId "com.midtrans.sdk.demo"
            resValue "string", "app_name", "Midtrans Demo"
            buildConfigField "String", "BASE_URL", MERCHANT_BASE_URL_PRODUCTION
            buildConfigField "String", "CLIENT_KEY", MERCHANT_CLIENT_KEY_PRODUCTION
            matchingFallbacks = ["production"]
        }

        uat {
            dimension "env"
            applicationId "com.midtrans.sdk.demo.uat"
            resValue "string", "app_name", "Midtrans uat dev"
            buildConfigField "String", "BASE_URL", MERCHANT_BASE_URL_STAGING
            buildConfigField "String", "CLIENT_KEY", MERCHANT_CLIENT_KEY_STAGING
            matchingFallbacks = ["uat"]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation supportLibraries.appCompat
    implementation supportLibraries.supportV4
    implementation supportLibraries.recyclerview
    implementation supportLibraries.cardView
    implementation supportLibraries.design
    implementation libraries.swipeLayout
    implementation(libraries.crashlytics) {
        transitive = true
    }
    implementation project(':uikit')
    implementation(project(':scancard')) {
        exclude module: 'uikit'
        exclude module: 'corekit'
    }
}