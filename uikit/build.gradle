apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply from: '../jacoco-uikit.gradle'
apply plugin: 'com.vanniktech.android.junit.jacoco'

group = 'com.midtrans'
version = '1.15.2'

ext {
    espressoVersion = "2.2.2"
    supporTestVersion = "0.5"
    junitVersion = "4.12"
}


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.4.0'

    }
}
repositories {
    jcenter()
    maven { url "http://dl.bintray.com/pt-midtrans/maven" }
}
android {
    def DEFAULT_PUBLISH
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion rootProject.ext.uiMinSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName version

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        //raygun api key
        resValue "string", "ISSUE_TRACKER_API_KEY", ISSUE_TRACKER_API_KEY
    }
    buildTypes {
        release {
            minifyEnabled true

            consumerProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            testCoverageEnabled = true;
        }

    }

    productFlavors {
        development {
            DEFAULT_PUBLISH = "developmentRelease"
        }

        staging {
            DEFAULT_PUBLISH = "stagingRelease"
        }

        production {
            DEFAULT_PUBLISH = "productionRelease"
        }
    }

    testOptions {
        unitTests {
            all {
                // configure the test JVM arguments
                jvmArgs '-noverify'
            }
        }
    }

    defaultPublishConfig = DEFAULT_PUBLISH
    publishNonDefault true
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3' //version required
}

dependencies {

    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    compile 'ru.rambler.android:swipe-layout:1.0.14'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'me.saket:better-link-movement-method:1.2'
    compile 'com.github.stfalcon:smsverifycatcher:0.3.1'
    compile 'com.midtrans:issuetracker:1.11.0'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0'
    developmentCompile project(path: ':corekit', configuration: 'developmentRelease')
    stagingCompile project(path: ':corekit', configuration: 'stagingRelease')
    productionCompile project(path: ':corekit', configuration: 'productionRelease')

    /*
     * test dependencies
     */

    // Android JUnit Runner
    androidTestCompile 'com.android.support.test:runner:0.5'

    testCompile 'junit:junit:4.12'

    testCompile 'org.mockito:mockito-core:1.10.19'

    testCompile('org.powermock:powermock-api-mockito:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testCompile('org.powermock:powermock-module-junit4:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }

    testCompile('org.powermock:powermock-api-mockito:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testCompile('org.powermock:powermock-module-junit4-rule:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }

    testCompile('org.powermock:powermock-classloading-xstream:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }

    androidTestCompile('org.powermock:powermock-mockito-release-full:1.6.2') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/uikit-development-release.aar")
            groupId group
            artifactId 'uikit'
            version version + "-SANDBOX"

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                configurations.developmentCompile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version + '-SANDBOX')
                }
            }
        }

        BintrayStagingPublication(MavenPublication) {
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/uikit-staging-release.aar")
            groupId group
            artifactId 'uikit'
            version version + "-STAGING"

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                configurations.stagingCompile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version + '-STAGING')
                }
            }
        }

        BintrayProductionPublication(MavenPublication) {
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/uikit-production-release.aar")
            groupId group
            artifactId 'uikit'
            version version

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                configurations.productionCompile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'pt-midtrans'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/veritrans/veritrans-android.git'
        desc = 'Android SDK for integrating with midtrans.com'
        websiteUrl = "https://midtrans.com"
        publish = true
        publicDownloadNumbers = true
    }
    publications = ['BintrayProductionPublication', 'BintrayPublication']

}
